buildscript {
  ext {
    springBootVersion = "1.1.9.RELEASE"
  }
  repositories {
    jcenter()
  }
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    classpath 'com.netflix.nebula:nebula-ospackage-plugin:1.12.+'
  }
}

apply plugin: 'spring-boot'
apply plugin: 'nebula-ospackage-application'
apply plugin: 'groovy'
apply plugin: 'idea'

repositories {
  jcenter()
  maven { url "http://repo.spring.io/snapshot" }
}

dependencies {
  compile project(":gate-manual")

  compile spinnaker.dependency("groovy")
  compile spinnaker.dependency("kork")
  compile spinnaker.dependency("frigga")

  compile 'org.springframework:springloaded:1.2.1.RELEASE'
  spinnaker.group('retrofitDefault')

  compile spinnaker.dependency('cglib')
  compile 'com.netflix.hystrix:hystrix-core:1.4.0-RC5'
  compile "org.springframework.boot:spring-boot-starter-security:${springBootVersion}"

  spinnaker.group("bootWeb")
  spinnaker.group("test")
}

configurations.all {
  exclude group: 'javax.servlet', module: 'servlet-api'
  exclude group: "org.slf4j", module: "slf4j-log4j12"
}

ideaConfig {
  mainClassName = 'com.netflix.spinnaker.gate.Main'
}

tasks.startScripts {
  doLast {
    def scriptFile = file "${outputDir}/${applicationName}"
    scriptFile.text = scriptFile.text.replace('CLASSPATH=$APP_HOME/lib', 'CLASSPATH=$APP_HOME/lib/int/*:$APP_HOME/lib')
  }
}

ospackage {
  packageName = "gate"
}

tasks.build.dependsOn = [buildDeb]

tasks.bootRepackage.enabled = false
