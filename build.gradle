buildscript {
  ext {
    springBootVersion = "1.2.8.RELEASE"
  }
  repositories {
    jcenter()
    maven { url "http://spinnaker.bintray.com/gradle" }
  }
  dependencies {
    classpath 'com.netflix.spinnaker.gradle:spinnaker-gradle-project:3.5.0'
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
  }
}

allprojects {
  apply plugin: 'spinnaker.project'
  apply plugin: 'groovy'

  ext {
    spinnakerDependenciesVersion = "0.54.0"
  }

  if (spinnakerDependenciesVersion.indexOf('-dev') != -1) {
    repositories {
      mavenLocal()
    }
  }

  spinnaker {
    dependenciesVersion = spinnakerDependenciesVersion
  }

  configurations.all {
    exclude group: 'javax.servlet', module: 'servlet-api'
    exclude group: 'javax.servlet', module: 'javax.servlet-api'
    resolutionStrategy {
      force 'org.codehaus.groovy:groovy-all:2.4.5'
      eachDependency {
        if (it.requested.group == 'org.springframework.boot') {
          it.useVersion '1.2.8.RELEASE'
        }
        if (it.requested.group == 'org.springframework') {
          it.useVersion '4.1.9.RELEASE'
        }
        if (it.requested.group == 'org.spockframework') {
          it.useVersion '1.1-groovy-2.4-rc-1'
        }
      }
    }
  }

  group = "com.netflix.spinnaker.gate"

  test {
    testLogging {
      exceptionFormat = 'full'
    }
  }
}

defaultTasks ':gate-web:bootRun'
